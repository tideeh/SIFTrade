apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'

// aqui configura o version code e o version name
ext.versionMajor = 1 // altera quando tem um grande releas
ext.versionMinor = 1 // altera quando tem um release medio (volta pra 0 quando altera o Major)
ext.versionPatch = 5 // altera em qualquer release (volta pra 0 quando altera o Minor)
ext.versionClassifier = "ALFA" // nome da versao (null) para desativar
ext.minimumSdkVersion = 21 // altera quando precisar aumentar o min SDK version (novas apis)

android {
    compileSdkVersion 29


    defaultConfig {
        applicationId "br.com.polenflorestal.siftrade"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 29
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation 'com.google.android.material:material:1.1.0'

    implementation 'com.google.firebase:firebase-analytics:17.4.1'
    implementation 'com.google.firebase:firebase-auth:19.3.1'
    implementation 'com.facebook.android:facebook-android-sdk:5.11.0'
    implementation 'com.facebook.android:facebook-login:5.11.0'
    implementation 'com.google.android.gms:play-services-auth:18.0.0'
    implementation 'com.google.firebase:firebase-firestore:21.4.3'
}
